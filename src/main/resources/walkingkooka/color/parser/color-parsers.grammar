(* rgb ...............................................................................................................*)
RGB_RGBA_FUNCTION    = RGB_RGBA,
                       PARENTHESIS_OPEN,
                       [WHITESPACE],
                       ( RGB_ARGUMENTS_PERCENTAGE_COMMA | RGB_ARGUMENTS_PERCENTAGE_WHITESPACE | RGB_ARGUMENTS_NUMBER_COMMA | RGB_ARGUMENTS_NUMBER_WHITESPACE ),
                       [WHITESPACE],
                       PARENTHESIS_CLOSE;

RGB_RGBA             = "rgba" | "rgb";

RGB_ARGUMENTS_PERCENTAGE_COMMA      = PERCENTAGE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                      PERCENTAGE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                      PERCENTAGE,
                                      [ [WHITESPACE], SEPARATOR, [WHITESPACE], ALPHA ];

RGB_ARGUMENTS_PERCENTAGE_WHITESPACE = PERCENTAGE, WHITESPACE,
                                      PERCENTAGE, WHITESPACE,
                                      PERCENTAGE,
                                      [ [WHITESPACE], "/", [WHITESPACE], ALPHA ];

RGB_ARGUMENTS_NUMBER_COMMA          = NUMBER, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                      NUMBER, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                      NUMBER,
                                      [ [WHITESPACE], SEPARATOR, [WHITESPACE], ALPHA ];

RGB_ARGUMENTS_NUMBER_WHITESPACE     = NUMBER, WHITESPACE,
                                      NUMBER, WHITESPACE,
                                      NUMBER,
                                      [ [WHITESPACE], "/", [WHITESPACE], ALPHA ];

(* hsl ...............................................................................................................*)
HSL_HSLA_FUNCTION = HSL_HSLA,
                    PARENTHESIS_OPEN,
                    [WHITESPACE],
                    ( HSL_HSV_ARGUMENTS_PERCENTAGE_COMMA | HSL_HSV_ARGUMENTS_PERCENTAGE_WHITESPACE | HSL_HSV_ARGUMENTS_NUMBER_COMMA | HSL_HSV_ARGUMENTS_NUMBER_WHITESPACE ),
                    [WHITESPACE],
                    PARENTHESIS_CLOSE;

HSL_HSLA          = "hsla" | "hsl";

(* hsv ...............................................................................................................*)
HSV_HSVA_FUNCTION = HSV_HSVA,
                    PARENTHESIS_OPEN,
                    [WHITESPACE],
                    ( HSL_HSV_ARGUMENTS_PERCENTAGE_COMMA | HSL_HSV_ARGUMENTS_PERCENTAGE_WHITESPACE | HSL_HSV_ARGUMENTS_NUMBER_COMMA | HSL_HSV_ARGUMENTS_NUMBER_WHITESPACE ),
                    [WHITESPACE],
                    PARENTHESIS_CLOSE;

HSV_HSVA          = "hsva" | "hsv";

(* hsl/hsv common ....................................................................................................*)

HSL_HSV_ARGUMENTS_PERCENTAGE_COMMA      = HUE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                          PERCENTAGE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                          PERCENTAGE,
                                          [ [WHITESPACE], SEPARATOR, [WHITESPACE], ALPHA ];

HSL_HSV_ARGUMENTS_PERCENTAGE_WHITESPACE = HUE, WHITESPACE,
                                          PERCENTAGE, WHITESPACE,
                                          PERCENTAGE,
                                          [ [WHITESPACE], "/", [WHITESPACE], ALPHA ];

HSL_HSV_ARGUMENTS_NUMBER_COMMA          = HUE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                          NUMBER, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                          NUMBER,
                                          [ [WHITESPACE], SEPARATOR, [WHITESPACE], ALPHA ];

HSL_HSV_ARGUMENTS_NUMBER_WHITESPACE     = HUE, WHITESPACE,
                                          NUMBER, WHITESPACE,
                                          NUMBER,
                                          [ [WHITESPACE], "/", [WHITESPACE], ALPHA ];

HUE                                     = ( NUMBER, "deg" ) |
                                          NUMBER;

(* common ............................................................................................................*)

PARENTHESIS_CLOSE = ")";
PARENTHESIS_OPEN  = "(";
SEPARATOR         = ",";
WHITESPACE        = " ";

PERCENTAGE        = NUMBER, "%";
ALPHA             = PERCENTAGE | NUMBER;